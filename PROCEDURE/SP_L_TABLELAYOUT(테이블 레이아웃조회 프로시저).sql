USE WOORIM_230215
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [DBO].[SP_L_TABLELAYOUT]
	@TBL	VARCHAR(50) = ''	-- 테이블명			
AS
/******************************************************************************
** FILE:
** NAME: SP_L_TABLELAYOUT
**
** DESC: 테이블 레이아웃 조회
**
** AUTH: 이상현
** DATE: 2022.04.18
*******************************************************************************
** CHANGE HISTORY
*******************************************************************************
** DATE:		AUTHOR:	DESCRIPTION:
** ---------	------	-----------------
*******************************************************************************/
SET NOCOUNT ON;

BEGIN
	BEGIN TRY
		SELECT	A.TABLE_NAME, 		 
				C.VALUE AS TABLE_COMMENT,
				A.COLUMN_NAME,
				A.DATA_TYPE, 
				ISNULL(CAST(A.CHARACTER_MAXIMUM_LENGTH AS VARCHAR),  
				CAST(A.NUMERIC_PRECISION AS VARCHAR) + ',' + CAST(A.NUMERIC_SCALE AS VARCHAR)) AS COLUMN_LENGTH,
				A.COLUMN_DEFAULT, A.IS_NULLABLE,
				B.VALUE AS COLUM_COMMENT,
				(
					SELECT 'KEY'
					FROM SYS.INDEX_COLUMNS IC WITH (NOWAIT)
						JOIN SYS.COLUMNS C WITH (NOWAIT) ON C.[OBJECT_ID] = IC.[OBJECT_ID] AND C.COLUMN_ID = IC.COLUMN_ID
					WHERE IC.IS_INCLUDED_COLUMN = 0
						AND IC.[OBJECT_ID] = OBJECT_ID(A.TABLE_NAME)
						AND IC.INDEX_ID = (SELECT UNIQUE_INDEX_ID FROM SYS.KEY_CONSTRAINTS WHERE PARENT_OBJECT_ID = OBJECT_ID(A.TABLE_NAME))
						AND C.NAME = A.COLUMN_NAME 
				) AS K,
				CASE WHEN ISNULL(C.VALUE, '') = '' 
					 THEN 'EXEC   SP_ADDEXTENDEDPROPERTY ''MS_DESCRIPTION'', '',,''' 
					 ELSE 'EXEC SP_UPDATEEXTENDEDPROPERTY ''MS_DESCRIPTION'', '''+ CONVERT(VARCHAR(100),C.VALUE) +''' ' 
				 END +' , ''USER'', DBO, ''TABLE'', ''' + A.TABLE_NAME + '''' AS CMT1,
				CASE WHEN ISNULL(B.VALUE, '') = '' 
					 THEN 'EXEC   SP_ADDEXTENDEDPROPERTY ''MS_DESCRIPTION'', '',,''' 
					 ELSE 'EXEC SP_UPDATEEXTENDEDPROPERTY ''MS_DESCRIPTION'', '''+ CONVERT(VARCHAR(100),B.VALUE) +''' ' 
				 END + ' , ''USER'', DBO, ''TABLE'', ''' + A.TABLE_NAME + ''', ''COLUMN'', ''' + A.COLUMN_NAME + '''' AS CMT,
				'{ HEADER: '''+ CONVERT(VARCHAR(100), B.VALUE) +''', NAME: '''+A.COLUMN_NAME+''', INDEX: '''+A.COLUMN_NAME+''', WIDTH: 40, ALIGN: ''CENTER'' },' AS JQGRID
		  FROM INFORMATION_SCHEMA.COLUMNS A
		  JOIN SYS.COLUMNS COL ON OBJECT_ID(A.TABLE_NAME) = COL.OBJECT_ID AND  A.COLUMN_NAME = COL.NAME
		  LEFT OUTER JOIN SYS.EXTENDED_PROPERTIES B ON COL.OBJECT_ID = B.MAJOR_ID AND COL.COLUMN_ID = B.MINOR_ID
		  LEFT OUTER JOIN (SELECT OBJECT_ID(OBJNAME) AS TABLE_ID, 
								  VALUE
							 FROM ::FN_LISTEXTENDEDPROPERTY(NULL, 'USER','DBO','TABLE',NULL, NULL, NULL)
						   ) C ON OBJECT_ID(A.TABLE_NAME) = C.TABLE_ID
		 WHERE A.TABLE_NAME = @TBL
		 ORDER BY A.ORDINAL_POSITION
		--ORDER BY A.TABLE_NAME, A.COLUMN_NAME 
	END TRY
BEGIN CATCH          
	
	-- 오류발생시오류목록테이블에정보저장 
	DECLARE @ERR_MSG VARCHAR(2000);
	SET @ERR_MSG = ERROR_MESSAGE();
	--EXEC SP_SYET_ERR_MSG @USER_ID, 'SP_L_TABLELAYOUT', @ERR_MSG; 

END CATCH
						
END
